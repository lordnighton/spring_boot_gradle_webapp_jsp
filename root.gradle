import com.gradle.helpers.GradlePrinter;

buildscript {
	ext {
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		jcenter()
		mavenCentral()
		maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath 'gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.15.0'
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    	classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6'
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'org.sonarqube'
apply plugin: 'org.hidetake.swagger.generator'

swaggerSources {
  petstore {
    inputFile = file('fapi.yml')
    code {
      language = 'spring'
    }
  }
}

bootRun {
	jvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n"]
	args += ["--server.port=8000"]
}

war {
	baseName = 'spring-boot-gradle-webapp-example'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

task props << {
    println("My property = " + project.external)
}

task beforePrint {
	doFirst {
		println "Before print"
	}
}

class VersionSpecifier {
	Integer major;
	Integer minor;

	VersionSpecifier(Integer major, Integer minor) {
		this.major = major
		this.minor = minor
	}

	@Override
	String toString() {
		"Major = $major, Minor = $minor"
	}
}

version = new VersionSpecifier(1,0)

task printVersion(dependsOn: [beforePrint]) {
	group = 'Versioning'
	description = 'Printing the version of a project'

	doLast {
		println "Project version = $version"
		logger.quiet "Logging the version = $version"
	}
}

task printLocalCacheDirs << {
	configurations.getByName('compile').each { dep -> 
		println dep
	}
}

task printBuildName << {
	GradlePrinter.printBuildName();
}

project.ext.fileToRead = file('fileToRead.txt')

task readFile() << {
	logger.quiet "Reading the file $project.fileToRead"

	if (project.fileToRead.exists()) {
		println "Exists"
		String fileContents = project.fileToRead.text
		println "Contents => [ $fileContents ]"
	} else {
		throw new GradleException("File not found -- $project.fileToRead")
	}
}

task packBundle(type: Zip) {
	archiveName 'bundle.zip'
	destinationDir = file("${projectDir}/dist")

	from "${projectDir}/src"

	doLast {
		println "Bundling is over"
	}
}

task removeDist(type: Delete) {
	delete = "${projectDir}/dist"
  	followSymlinks = true
}

test {
	testLogging {
		showStandardStreams = true
		exceptionFormat 'full'
		events 'started', 'passed', 'skipped', 'failed'
	}
}

dependencies {
	swaggerCodegen 'io.swagger:swagger-codegen-cli:2.3.1'

	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.apache.commons:commons-lang3:3.0')
	compile('javax.servlet:jstl')

	// Unit tests
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.+'

	// Integration tests
	testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.2.0'

	providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
	providedRuntime('org.apache.tomcat.embed:tomcat-embed-jasper')
}

task integrationTests(type: Test) {
	include 'RestAssuredTest.class'
	reports.html.destination = file('$reports.html.destination/integration')
}

check.dependsOn integrationTests

task sayHello << {
	println "Hello from ROOT project"
}

ant.importBuild("ant/build.xml")

task helloFromAnt {
    doLast {
        String greeting = 'hello from Ant'
        ant.echo(message: greeting)
    }
}

hello {
	doFirst {
		println "[before] Ant hello task"
	}
	doLast {
        println "[after] Ant hello task"
    }
}